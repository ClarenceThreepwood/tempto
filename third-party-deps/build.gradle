/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

configurations {
  hiveJdbcFatJar
  provided
}

dependencies {
  hiveJdbcFatJar libraries.hive_jdbc
  hiveJdbcFatJar (libraries.hadoop_core) {transitive=false}
  provided libraries.presto_jdbc
}

task hiveJdbcFatJar(type: Jar, dependsOn: clean) {
  baseName = 'hive-jdbc'
  appendix = 'all'
  from {
    configurations.hiveJdbcFatJar.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
}

task copyProvidedDeps(type: Copy, dependsOn: hiveJdbcFatJar) {
  from configurations.provided
  into ("$buildDir/libs")
  rename ('presto-jdbc-(.*).jar', 'presto-jdbc-all.jar')
}

build.dependsOn.add([hiveJdbcFatJar, copyProvidedDeps])

publishing {
  publications {
    hiveJdbcFatJar(MavenPublication) {
      groupId 'org.apache.hive'
      artifactId 'hive-jdbc-all'
      version '1.0-SNAPSHOT'

      artifacts = ["$libsDir/hive-jdbc-all.jar"]
    }
    prestoJdbcFatJar(MavenPublication) {
      groupId 'com.facebook.presto'
      artifactId 'presto-jdbc-all'
      version versions['presto'] + '-SNAPSHOT'

      artifacts = ["$libsDir/presto-jdbc-all.jar"]
    }
  }
  repositories publish_repositories
}
