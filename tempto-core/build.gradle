/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.2"
  }
}

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'maven-publish'

dependencies {
  compile libraries.assertj
  compile libraries.guava
  compile libraries.guice
  compile libraries.testng
  compile libraries.commons_lang3
  compile libraries.commons_dbcp2
  compile libraries.slf4j_api
  compile libraries.snakeyaml
  compile libraries.httpclient
  compile libraries.ioutils
  compile libraries.tpchgen
  compile libraries.jsonpath
  compile libraries.bytebuddy
  compile libraries.reflections
  compile libraries.jsch
  compile libraries.freemarker

  testCompile libraries.spock
  testCompile libraries.hsqldb
  testCompile libraries.dbutils
  testCompile libraries.mina_sshd
}

// project information
group 'com.teradata.tempto'
archivesBaseName = 'teradata-tempto'
version tempto_version

javadoc {
  failOnError = false
}

// javadoc.jar generation
task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

// sources.jar generation
task sourcesJar (type : Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

artifacts {
  archives javadocJar, sourcesJar
}

// publishing to our nexus

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'com.teradata.tempto'
      artifactId 'framework-logging-log4j'
      version tempto_version
      from components.java
      artifact sourcesJar {
        classifier 'sources'
      }
    }
  }
  repositories publish_repositories
}

task buildFatJar(type: Jar, dependsOn: assemble) {
  appendix = 'all'
  from sourceSets.main.output
  from {
    configurations.runtime.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
}

license {
  exclude "**/convention/sample-test/**/*.sql"
}

build.dependsOn.add(buildFatJar)

// publishing to sonatype

nexusStaging {
  packageGroup = group
  stagingProfileId = '176117bc76f90b'
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      pom.project {
        name 'tempto-core'
        packaging 'jar'
        artifactId 'tempto-core'
        description 'Tempto - test framework'
        url 'https://github.com/prestodb/tempto'
        scm {
          url 'git@github.com:prestodb/tempto.git'
          connection 'scm:git:git@github.com:prestodb/tempto.git'
          developerConnection 'scm:git:git@github.com:prestodb/tempto.git'
        }
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/license/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id 'bdch'
            name 'Boston Development Center for Hadoop'
            email 'EB230060 aat teradata doot com'
          }
        }
      }
    }
  }
}